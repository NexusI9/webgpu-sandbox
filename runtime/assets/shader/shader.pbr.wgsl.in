// attribute/uniform decls
// WGSL Function references:
// https://webgpufundamentals.org/webgpu/lessons/webgpu-wgsl-function-reference.html#func-textureSampleCompareLevel

// constants
const PI : f32 = 3.14159265359;
const POINT_LIGHT_VIEWS : u32 = 6u;
const MAX_LIGHT : u32 = 16u;
const PCF_KERNEL_SIZE : u32 = 2u;

struct VertexIn {
  @location(0) aPos : vec3<f32>,
                      @location(1) aNorm : vec3<f32>,
                                           @location(2) aCol : vec3<f32>,
                                                               @location(3) aUv
      : vec2<f32>,
};

struct VertexOut {
  @builtin(position) vPosition : vec4<f32>,
                                 @location(0) vNormal : vec3<f32>,
                                                        @location(1) vCol
      : vec3<f32>,
        @location(2) vUv : vec2<f32>,
                           @location(3) vFrag : vec3<f32>,
};

struct Mesh {
  model : mat4x4<f32>, position : vec4<f32>,
}

struct Camera {
  view : mat4x4<f32>,
         position : vec4<f32>,
                    lookat : vec4<f32>,
                             mode : u32,
                                    _pad : vec3<u32>,
};

struct Viewport {
  projection : mat4x4<f32>,
};

struct Material {
  albedo : vec3<f32>,
           metallic : f32,
                      roughness : f32,
                                  normal : vec3<f32>,
                                           occlusion : f32,
                                                       emissive : vec3<f32>,
};

// Lights
struct PointLight {
  position : vec3<f32>,
             cutoff : f32,
                      color : vec3<f32>,
                              intensity : f32,
                                          views : array<mat4x4<f32>, 6>,
                                                  inner_cutoff : f32,
                                                                 near : f32,
                                                                        far
      : f32,
        _padding : f32,
};

struct AmbientLight {
  color : vec3<f32>, intensity : f32,
};

struct SpotLight {
  position : vec3<f32>,
             cutoff : f32,
                      lookat : vec3<f32>,
                               inner_cutoff : f32,
                                              color : vec3<f32>,
                                                      intensity : f32,
                                                                  view
      : mat4x4<f32>
};

struct SunLight {
  position : vec3<f32>,
             intensity : f32,
                         color : vec3<f32>,
                                 _padding : f32,
                                            view : mat4x4<f32>,
};

struct AmbientLightStorage {
  length : u32, items : array<AmbientLight, MAX_LIGHT>,
};

struct PointLightStorage {
  length : u32, items : array<PointLight, MAX_LIGHT>,
};

struct SpotLightStorage {
  length : u32, items : array<SpotLight, MAX_LIGHT>,
};

struct SunLightStorage {
  length : u32, items : array<SunLight, MAX_LIGHT>,
};

// material pbr
// 1. texture : The actual image data stored in VRAM (pixel colors, normal maps,
// etc.)
// 2. sampler : Defines how the GPU reads the texture (filtering, wrapping,
// mipmaps, etc.)
// TODO: combine textures in a texture_2d_array + only use 1 sampler ?

@group(0) @binding(0) var diffuse_texture : texture_2d<f32>;
@group(0) @binding(1) var diffuse_sampler : sampler;

@group(0) @binding(2) var metallic_texture : texture_2d<f32>;
@group(0) @binding(3) var metallic_sampler : sampler;

@group(0) @binding(4) var normal_texture : texture_2d<f32>;
@group(0) @binding(5) var normal_sampler : sampler;

@group(0) @binding(6) var emissive_texture : texture_2d<f32>;
@group(0) @binding(7) var emissive_sampler : sampler;

@group(0) @binding(8) var occlusion_texture : texture_2d<f32>;
@group(0) @binding(9) var occlusion_sampler : sampler;

// camera viewport
@group(1) @binding(0) var<uniform> uViewport : Viewport;
@group(1) @binding(1) var<uniform> uCamera : Camera;
@group(1) @binding(2) var<uniform> uMesh : Mesh;

// light
// TODO: using uniform for now, but check the storage type
@group(2) @binding(0) var<uniform> ambient_light_list : AmbientLightStorage;
@group(2) @binding(1) var<uniform> spot_light_list : SpotLightStorage;
@group(2) @binding(2) var<uniform> point_light_list : PointLightStorage;
@group(2) @binding(3) var<uniform> sun_light_list : SunLightStorage;

#ifdef RENDER_SHADOW_AS_COLOR
@group(2) @binding(4) var point_shadow_maps : texture_cube_array<f32>;
@group(2) @binding(5) var point_shadow_sampler : sampler;
@group(2) @binding(6) var directional_shadow_maps : texture_2d_array<f32>;
@group(2) @binding(7) var directional_shadow_sampler : sampler;
#else
@group(2) @binding(4) var point_shadow_maps : texture_depth_cube_array;
@group(2) @binding(5) var point_shadow_sampler : sampler_comparison;
@group(2) @binding(6) var directional_shadow_maps : texture_depth_2d_array;
@group(2) @binding(7) var directional_shadow_sampler : sampler_comparison;
#endif

// vertex shader
@vertex fn vs_main(input : VertexIn) -> VertexOut {

  // Final Matrix (Projection * View)
  var cam : mat4x4<f32> = uViewport.projection * uCamera.view;
  var output : VertexOut;

  output.vPosition = cam * uMesh.model * vec4<f32>(input.aPos, 1.0f);
  output.vNormal = normalize(input.aNorm);
  output.vCol = input.aCol;
  output.vUv = input.aUv;

  // transform vertex position (object local position) to world space
  // used for lightning to know where the vertex is in the world space
  // if only used model, vertex position is "constriained" its own local space
  output.vFrag = (uMesh.model * vec4<f32>(input.aPos, 1.0f)).xyz;

  return output;
}

// Fresnel usin Schlick Approximation
fn f_schlick(F0 : vec3<f32>, HdotV : f32) -> vec3<f32> {
  // Metallic surface : F0 = albedo
  // Dielectric surface : F0 = vec3(0.4) (plastic/ wood...)
  return F0 + (1.0f - F0) * pow(1.0f - HdotV, 5.0);
}

fn distribution_GGX(NdotH : f32, roughness : f32) -> f32 {
  var a = roughness * roughness;
  var a2 = a * a;
  var NdotH2 = NdotH * NdotH;

  var num = a2;
  var denom = (NdotH2 * (a2 - 1.0f) + 1.0f);
  denom = PI * denom * denom;
  return num / denom;
}

fn geo_schlick_ggx(NdotV : f32, roughness : f32) -> f32 {
  var r = (roughness + 1.0f);
  var k = (r * r) / 8.0f;
  return NdotV / (NdotV * (1.0f - k) + k);
}

fn geo_smith(NdotV : f32, NdotL : f32, roughness : f32) -> f32 {
  return geo_schlick_ggx(NdotV, roughness) * geo_schlick_ggx(NdotL, roughness);
}

fn dot_max(A : vec3<f32>, B : vec3<f32>) -> f32 { return max(dot(A, B), 0.0f); }

fn create_material(diffuse : vec4<f32>, metallic : vec4<f32>,
                   normal : vec4<f32>, occlusion : vec4<f32>,
                   emissive : vec4<f32>) -> Material {

  var material : Material;
  material.albedo = diffuse.rgb;
  material.metallic = metallic.b;
  material.roughness = metallic.g;                     // roughness in G channel
  material.normal = normalize(normal.rgb * 2.0 - 1.0); // convert to [-1,+1]
  material.occlusion = occlusion.b;
  material.emissive = emissive.rgb;

  return material;
}

fn compute_point_light(material : Material, fragment_position : vec3<f32>,
                       vertex_normal : vec3<f32>, camera_position : vec3<f32>,
                       light_position : vec3<f32>, light_color : vec3<f32>,
                       light_intensity : f32) -> vec3<f32> {

  // compute light base on Cook-Torrance BRDF Model

  // 1. Normalize key vectors
  var N : vec3<f32> = normalize(material.normal); // surface normal
  var V : vec3<f32> =
              normalize(camera_position - fragment_position); // view vector

  // transform normal to worldspace
  let world_normal = (uMesh.model * vec4<f32>(vertex_normal, 0.0f)).xyz;

  // make dot product to check if normal point towards light or not
  var L : vec3<f32> =
              vec3<f32>(dot(normalize(world_normal),
                            normalize(light_position - fragment_position)));

  var H : vec3<f32> = normalize(V + L); // halfway vector

  // 2. Fresnel effect
  // Determines how much light reflects vs. refracts
  var HdotV : f32 = dot_max(H, V);
  var F0 : vec3<f32> = mix(vec3(0.04), material.albedo, material.metallic);
  var F : vec3<f32> = f_schlick(F0, HdotV);

  // 3. Normal Distribution Function - NDF (GGX/ Trowbridge-Reitz)
  // Determine how surface distribute microfacet (i.e. bumps)
  var NdotH : f32 = dot_max(N, H);
  var D : f32 = distribution_GGX(NdotH, material.roughness);

  // 4.Geometry Function (Schlick-GGX)
  //  Handles shadow and masking
  var NdotV : f32 = dot_max(N, V);
  var NdotL : f32 = dot_max(N, L);
  var G = geo_smith(NdotV, NdotL, material.roughness);

  // 5. Define Cook-Torrance Specular Reflection
  var specular : vec3<f32> = (D * F * G) / max(4.0f * NdotV * NdotL, 0.001);

  // 6. Compute Diffuse & Final lighting
  // Metallic surface only use the specular term
  // Non-Metallic surface use both diffuse + specular
  var kS = F;
  var kD = vec3(1.0f) - kS;
  kD *= 1.0f - material.metallic;

  var diffuse : vec3<f32> = (material.albedo / PI);
  var Lo : vec3<f32> = (kD * diffuse + specular) * light_color *
                       light_intensity * NdotL;

  // 7. Apply AO //0.03f
  // change first operator to alter ambient light

  return vec3<f32>(Lo);
}

fn compute_ambient_light(material : Material, light_color : vec3<f32>,
                         light_intensity : f32) -> vec3<f32> {
  return vec3(light_intensity) * material.albedo * material.occlusion;
}

fn compute_point_depth(position : f32, near : f32, far : f32) -> f32 {

  // let norm_z_comp = (far + near) / (far - near) -
  //                   (2.0f * far * near) / (far - near) / local_z_comp;
  // let depth = norm_z_comp - 0.002f;

  let norm_z_comp = (-position * -1.0f * (far + near)) / (far - near) +
                    ((-2.0f * far * near) / (far - near));

  let depth = (far + near) / (far - near) -
              2.0f * far * near / (norm_z_comp * (far - near));

  return (depth + 1.0f) * 0.5f;
}

// Combine and compute Point light shadow
fn point_shadow_factor(frag_position : vec3<f32>) -> f32 {

  if (point_light_list.length == 0u) {
    return 0.0f;
  }

  var factor : f32 = 1.0f; // show by default
  var bias : f32 = 0.002f;

  for (var l : u32 = 0u; l < point_light_list.length; l++) {

    let light = point_light_list.items[l];
    let light_position = light.position;

    var frag_to_light = frag_position - light_position.xyz;

    let current_depth = length(frag_to_light);

    let far = 100.0f;
    let near = 0.1f;

#ifdef RENDER_SHADOW_AS_COLOR

    var closest_depth = textureSample(point_shadow_maps, point_shadow_sampler,
                                      normalize(frag_to_light), l)
                            .r;

    closest_depth = closest_depth * far;

    if (current_depth - bias > closest_depth) {
      factor = 0.0f;
    } else {
      factor = 1.0f;
    }

#else

    let abs_vec = abs(frag_to_light);
    let local_z_comp = max(abs_vec.x, max(abs_vec.y, abs_vec.z));
    var shadow_direction = normalize(frag_to_light);

    let depth = compute_point_depth(local_z_comp, light.near, light.far);

    let distance = length(frag_to_light) / light.far;

    if (distance > light.cutoff) {
      factor = 0.0f;
    } else {
      factor = smoothstep(light.cutoff, light.inner_cutoff, distance);
      factor = pow(factor, light.intensity);
    }

    // PCF
    var pcf_shadow_factor = 0.0f;
    var pcf_sample_count = 0u;
    let pcf_kernel_size = PCF_KERNEL_SIZE;
    let pcf_half_kernel_size : f32 = f32(pcf_kernel_size / 2u);
    let spread = 0.05f;

    for (var x = -pcf_half_kernel_size; x <= pcf_half_kernel_size; x += 1.0f) {
      for (var y = -pcf_half_kernel_size; y <= pcf_half_kernel_size;
           y += 1.0f) {
        for (var z = -pcf_half_kernel_size; z <= pcf_half_kernel_size;
             z += 1.0f) {

          let offset = vec3(x, y, z) / light.far * spread;
          let sample_direction = shadow_direction + offset;
          pcf_shadow_factor +=
              textureSampleCompare(point_shadow_maps, point_shadow_sampler,
                                   sample_direction, l, depth - bias);
          pcf_sample_count += 1u;
        }
      }
    }

    pcf_shadow_factor /= f32(pcf_sample_count);
    factor *= pcf_shadow_factor;

#endif
  }

  return factor;
}

//  Compute Directional light shadow

fn directional_shadow_position(world_position : vec3<f32>,
                               light_view : mat4x4<f32>) -> vec3<f32> {

  // transform world position into clip space
  let shadow_clip = light_view * vec4<f32>(world_position, 1.0f);

  // convert to NDC coordinates
  var shadow_ndc = shadow_clip.xyz / shadow_clip.w;

  // convert to UV (0-1 range)
  let shadow_uv = shadow_ndc.xy * vec2<f32>(0.5f, -0.5f) + 0.5f;
  let shadow_depth = shadow_ndc.z;

  // returns depth to compare
  return vec3<f32>(shadow_uv, shadow_depth);
}

fn spot_shadow_factor(frag_position : vec3<f32>) -> f32 {

  if (spot_light_list.length == 0u) {
    return 0.0f;
  }

  var factor : f32 = 1.0f; // show by default
  var bias : f32 = 0.0005f;

  for (var l : u32 = 0u; l < spot_light_list.length; l++) {

    let light = spot_light_list.items[l];
    let shadow_position =
        directional_shadow_position(frag_position, light.view);

#ifdef RENDER_SHADOW_AS_COLOR
    factor *= textureSample(directional_shadow_maps, directional_shadow_sampler,
                            shadow_position.xy, l)
                  .r;

#else

    let frag_to_light = light.position - frag_position;
    let light_direction = normalize(frag_to_light);
    let spot_dot =
        dot(-light_direction, normalize(light.lookat - light.position));

    let shadow_depth = shadow_position.z - bias;

    if (spot_dot < light.cutoff) {
      factor = 0.0f;
    } else {
      factor = smoothstep(light.cutoff, light.inner_cutoff, spot_dot);
      factor = pow(factor, light.intensity);
    }

    // PCF
    var pcf_shadow_factor = 0.0f;
    var pcf_sample_count = 0u;
    let pcf_kernel_size = PCF_KERNEL_SIZE;
    let pcf_half_kernel_size : f32 = f32(pcf_kernel_size / 2u);
    let spread = 1.0f;

    for (var x = -pcf_half_kernel_size; x <= pcf_half_kernel_size; x += 1.0f) {
      for (var y = -pcf_half_kernel_size; y <= pcf_half_kernel_size;
           y += 1.0f) {

        let offset_uv = vec2(x, y) /
                        f32(textureDimensions(directional_shadow_maps).x) *
                        spread;

        let sample_uv = shadow_position.xy + offset_uv;
        pcf_shadow_factor += textureSampleCompare(directional_shadow_maps,
                                                  directional_shadow_sampler,
                                                  sample_uv, l, shadow_depth);
        pcf_sample_count += 1u;
      }
    }

    pcf_shadow_factor /= f32(pcf_sample_count);
    factor *= pcf_shadow_factor;

#endif
  }

  return factor;
}

fn sun_shadow_factor(frag_position : vec3<f32>) -> f32 {

  if (sun_light_list.length == 0u) {
    return 0.0f;
  }

  var factor : f32 = 1.0f; // show by default
  var bias : f32 = 0.005f;

  for (var l : u32 = 0u; l < sun_light_list.length; l++) {

    let layer = spot_light_list.length + l;
    let light = sun_light_list.items[l];
    let shadow_position =
        directional_shadow_position(frag_position, light.view);

#ifdef RENDER_SHADOW_AS_COLOR
    factor *= textureSample(directional_shadow_maps, directional_shadow_sampler,
                            shadow_position.xy, l)
                  .r;

#else

    let frag_to_light = light.position - frag_position;
    let light_direction = normalize(frag_to_light);
    let shadow_depth = shadow_position.z - bias;

    // PCF
    var pcf_shadow_factor = 0.0f;
    var pcf_sample_count = 0u;
    let pcf_kernel_size = PCF_KERNEL_SIZE;
    let pcf_half_kernel_size : f32 = f32(pcf_kernel_size / 2u);
    let spread = 1.0f;

    for (var x = -pcf_half_kernel_size; x <= pcf_half_kernel_size; x += 1.0f) {
      for (var y = -pcf_half_kernel_size; y <= pcf_half_kernel_size;
           y += 1.0f) {

        let offset_uv = vec2(x, y) /
                        f32(textureDimensions(directional_shadow_maps).x) *
                        spread;

        let sample_uv = shadow_position.xy + offset_uv;
        pcf_shadow_factor += textureSampleCompare(
            directional_shadow_maps, directional_shadow_sampler, sample_uv,
            layer, shadow_depth);
        pcf_sample_count += 1u;
      }
    }

    pcf_shadow_factor /= f32(pcf_sample_count);
    factor *= pcf_shadow_factor;

#endif
  }

  return factor;
}

// fragment shader
@fragment fn fs_main(@location(0) vNormal : vec3<f32>,
                     @location(1) vCol : vec3<f32>,
                     @location(2) vUv : vec2<f32>,
                     @location(3) vFrag : vec3<f32>) -> @location(0) vec4<f32> {

  var albedo = textureSample(diffuse_texture, diffuse_sampler, vUv);
  var metallic = textureSample(metallic_texture, metallic_sampler, vUv);
  var normal = textureSample(normal_texture, normal_sampler, vUv);
  var occlusion = textureSample(occlusion_texture, occlusion_sampler, vUv);
  var emissive = textureSample(emissive_texture, emissive_sampler, vUv);

  let material = create_material(albedo, metallic, normal, occlusion, emissive);

  let coord = vec2<i32>(vUv);

  var light_pos = vec3<f32>(0.0f);
  var light_color = vec3<f32>(0.0f);
  var Lo : vec3<f32> = vec3<f32>(0.0f);
  var ambient : vec3<f32> = vec3<f32>(0.0f);

  // calculate point lights
  for (var i = 0u; i < point_light_list.length; i++) {
    let light = point_light_list.items[i];
    Lo += compute_point_light(material, vFrag, vNormal, uCamera.position.xyz,
                              light.position, light.color, light.intensity);
  }

  // calculate spot lights
  // basically the same a point light, but use a fixed light direction instead
  // of a one per fragment like a point light
  for (var i = 0u; i < spot_light_list.length; i = i + 1u) {
    let light = spot_light_list.items[i];
    let light_direction = -1.0f * light.lookat - light.position;
    Lo += compute_point_light(material, vFrag, vNormal, uCamera.position.xyz,
                              light_direction, light.color, light.intensity);
  }

  // calculate sun lights
  for (var i = 0u; i < sun_light_list.length; i = i + 1u) {
    let light = sun_light_list.items[i];
    Lo += compute_point_light(material, vFrag, vNormal, uCamera.position.xyz,
                              light.position, light.color, light.intensity);
  }

  // calulate ambient lights
  var ambient_intensity = 0.0f;
  for (var i = 0u; i < ambient_light_list.length; i = i + 1u) {
    let light = ambient_light_list.items[i];
    ambient += compute_ambient_light(material, light.color, light.intensity);
    ambient_intensity += light.intensity;
  }

  // shadow map computation
  let point_shadow = point_shadow_factor(vFrag);
  let directional_shadow = spot_shadow_factor(vFrag);
  let sun_shadow = sun_shadow_factor(vFrag);

  let combined_shadow = mix(vec3<f32>(ambient_intensity), vec3<f32>(1.0),
                            point_shadow + directional_shadow + sun_shadow);

  //return vec4<f32>(ambient + Lo * combined_shadow, 1.0f);

  return vec4<f32>(occlusion.rgb, 1.0f);
}
